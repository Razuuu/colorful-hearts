plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom.runs.configureEach {
    // prevent loom from making run configurations for API
    ideConfigGenerated(false)
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    // Fabric
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"

    common(project(path: ':api:common', configuration: 'namedElements')) { transitive false }
    shadowBundle(project(path: ':api:common', configuration: 'transformProductionFabric')) { transitive false }
}

remapJar {
    input.set shadowJar.archiveFile
    injectAccessWidener = false
}

shadowJar {
    exclude "architectury.common.accesswidener"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

sourcesJar {
    afterEvaluate {
        def sourcesTask = project(":api:common").sourcesJar
        dependsOn sourcesTask
        from sourcesTask.archiveFile.map { zipTree(it) }
    }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
