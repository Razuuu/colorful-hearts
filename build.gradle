plugins {
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'dev.architectury.loom' version '1.6-SNAPSHOT' apply false
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id "com.modrinth.minotaur" version "2.+" apply false
    id 'net.darkhax.curseforgegradle' version '1.1.+' apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

allprojects {
    group = "terrails.colorfulhearts"
    version = rootProject.mod_version
}

// all subprojects
configure(subprojects.findAll { it.name != "api" }) {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'

    loom {
        accessWidenerPath = project(":common").file("src/main/resources/colorfulhearts.accesswidener")
    }

    base {
        // colorfulhearts-loader-version
        archivesBaseName = "colorfulhearts-${project.name}-${rootProject.minecraft_version}"
    }

    repositories {
        maven { url = "https://maven.parchmentmc.org" }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment_version}@zip" as String)
        }
    }

    java {
        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release = 21
    }
}

// all non-api subprojects
configure(subprojects.findAll { !it.path.startsWith(":api") }) {
    repositories {
        maven { url "https://maven.ryanliptak.com/" }
        maven { url "https://maven.shedaniel.me/" }
        exclusiveContent {
            forRepository {
                maven {
                    name = "Modrinth"
                    url = "https://api.modrinth.com/maven"
                }
            }
            filter {
                includeGroup "maven.modrinth"
            }
        }
    }

    tasks.register('printChangelog') {
        doLast {
            println(fetchChangelog())
        }
    }

    // From https://github.com/architectury/architectury-api/blob/7f76087ffb453a7b1c65ce53a8e0ed05745575aa/build.gradle#L93
    ext.fetchChangelog = {
        def branch = "git rev-parse --abbrev-ref HEAD".execute().in.text.trim()
        if (branch.isBlank() || branch == "HEAD") {
            throw new GradleException("Branch name could not be fetched.")
        }

        def repoUrl = "https://github.com/terrails/colorful-hearts"
        def changes = new StringBuilder("Latest changes ([See all](${repoUrl}/commits/${branch}))\n---")
        def log = "git log --max-count=30 --no-merges --pretty=format:\"* %s ([%h](${repoUrl}/commit/%H))\"".execute()
        log.in.eachLine { line ->
            if (!line.startsWithIgnoreCase("bump")) { // version change commits should be ignored
                changes << "\n${line}"
            }
        }

        if (log.waitFor() == 0) {
            return changes.toString()
        } else {
            throw new GradleException("Received an error while executing git log.")
        }
    }
}

// all non-common subprojects
configure(subprojects.findAll { it.name != "common" && it.name != "api" }) {
    apply plugin: 'maven-publish'

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = rootProject.group
                artifactId = "colorfulhearts-${project.name}${project.path.startsWith(':api') ? "-api" : ""}"
                from components.java
                // From https://github.com/Fuzss/modresources/blob/d40b8125cadb6ab1b56d90c03eb0c09a666d949a/gradle/v2/main.gradle#L235-L260
                pom {
                    name = "Colorful Hearts"
                    description = "A client side mod that replaces multiple vanilla heart rows with a single row using colored hearts"
                    url = "https://github.com/Terrails/colorful-hearts"
                    scm {
                        url = "https://github.com/Terrails/colorful-hearts"
                        connection = "scm:git:git://github.com/Terrails/colorful-hearts.git"
                        developerConnection = "scm:git:git@github.com:Terrails/colorful-hearts.git"
                    }
                    issueManagement {
                        system = "github"
                        url = "https://github.com/Terrails/colorful-hearts/issues"
                    }
                    licenses {
                        license {
                            name = "MIT"
                            url = "https://spdx.org/licenses/MIT.html"
                        }
                    }
                    developers {
                        developer {
                            id = "terrails"
                            name = "Terrails"
                        }
                    }
                }
            }
        }

        if (System.getenv("MODMAVEN_PATH")) {
            def mavenPath = System.getenv("MODMAVEN_PATH")
            if (file(mavenPath).exists()) {
                repositories {
                    maven {
                        name = "ModMaven"
                        url "file://" + System.getenv("MODMAVEN_PATH")
                    }
                }
            }
        }
    }
}
